plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'jacoco'
}

archivesBaseName = "read-resolve-check"
group 'com.stylingandroid.readresolve'
version '1.0.0'

dependencies {
    compileOnly Libraries.kotlinStdLib
    compileOnly Libraries.lintApi
    testImplementation Libraries.lintTests
    testImplementation Libraries.jupiter
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes("Lint-Registry-v2": "com.stylingandroid.readresolve.lint.LibraryIssueRegistry")
    }
}

tasks.test {
    useJUnitPlatform {
        includeEngines 'junit-jupiter'
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled true
        csv.enabled false
    }
}

apply from: '../publish.gradle'
